/*
    Generate Lexer and Parser tasks
 */

task generateHtlLexer(type: JavaExec) {
    group = "lexerparser"

    def src = "$project.projectDir/src/main/kotlin/com/aemtools/lang/htl/lexer/Htl.flex"
    def dst = "$project.projectDir/src/main/gen/com/aemtools/lang/htl/lexer/"

    main = "jflex.Main"

    args = ["--skel", "src/main/kotlin/com/aemtools/lang/htl/lexer/HtlLexer.skeleton",
            "-d", dst,
            src]

    inputs.file file(src)
    outputs.dir file(dst + "_HtlLexer.java")

    classpath configurations.gen
}

task generateHtlPsiAndParser(type: JavaExec) {
    group = "lexerparser"

    def src = "$project.projectDir/src/main/kotlin/com/aemtools/lang/htl/Htl.bnf"
    def dst = "$project.projectDir/src/main/gen"

    main = "org.intellij.grammar.Main"

    args = [ dst, file(src) ]

    inputs.file file(src)
    outputs.dir file(dst + "/com/aemtools/lang/htl")

    classpath   configurations.gen
}

task generateLexerAndParser {
    dependsOn generateHtlLexer
    dependsOn generateHtlPsiAndParser
    group = "lexerparser"
}